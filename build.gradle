/*
 * Copyright 2017-2021 Pranav Pandey
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    ext.versions = [
            'compileSdk': 31,
            'minSdk'    : 14,
            'targetSdk' : 31,
            'buildTools': '31.0.0',
            'androidx'  : '1.7.0-beta02'
    ]

    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.0-alpha13'
    }
}

plugins {
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    projectName = 'dynamic-utils'
    projectDesc = 'A collection of static methods and packages to perform dynamic ' +
            'operations on Android.'
    versionDesc = 'A collection of static methods and packages to perform dynamic ' +
            'operations on Android 4.0 (API 14) and above.'
    referenceTitle = 'Dynamic Utils API reference'

    siteUrl = 'https://github.com/pranavpandey/dynamic-utils'
    gitUrl = 'https://github.com/pranavpandey/dynamic-utils'
    issueUrl = 'https://github.com/pranavpandey/dynamic-utils/issues'
    githubUrl = 'pranavpandey/dynamic-utils'

    mavenRepo = 'android'
    mavenGroup = 'com.pranavpandey.android'
    mavenDir = 'com/pranavpandey/android'
    mavenArtifactId = 'dynamic-utils'
    mavenInceptionYear = '2017'
    mavenVersion = '4.3.0'
    mavenVersionCode = 32

    developerId = 'pranavpandey'
    developerName = 'Pranav Pandey'
    developerEmail = 'pranavpande92@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    licenseDistribution = 'repo'
    allLicenses = ["Apache-2.0"]

    publication = 'local.properties'

    ext["signing.keyId"] = ''
    ext["signing.password"] = ''
    ext["signing.secretKeyRingFile"] = ''

    ossrhUsername = ''
    ossrhPassword = ''
    sonatypeStagingProfileId = ''
}

apply plugin: 'io.github.gradle-nexus.publish-plugin'

File publish = project.rootProject.file("${publication}")
if (publish.exists()) {
    Properties properties = new Properties()
    new FileInputStream(publish).withCloseable { is -> properties.load(is) }
    properties.each { name, value -> ext[name] = value }
}

nexusPublishing {
    repositories {
        sonatype {
            username = ossrhUsername
            password = ossrhPassword
            stagingProfileId = sonatypeStagingProfileId
        }
    }
}

/*
 * Copyright 2017-2025 Pranav Pandey
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    ext.versions = [
            'compileSdk': 36,
            'minSdk'    : 21,
            'targetSdk' : 36,
            'buildTools': '36.0.0',
            'androidx'  : '1.16.0',
            'kotlin'    : '1.9.24'
    ]

    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.10.1'
    }
}

plugins {
    id("io.github.gradle-nexus.publish-plugin") version "2.0.0"
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

tasks.register('clean', Delete) {
    delete rootProject.layout.buildDirectory
}

ext {
    projectName = 'dynamic-utils'
    projectDesc = 'A collection of static methods and packages to perform dynamic ' +
            'operations on Android.'
    versionDesc = 'A collection of static methods and packages to perform dynamic ' +
            'operations on Android 4.0 (API 14) and above.'
    referenceTitle = 'Dynamic Utils API reference'

    siteUrl = 'https://github.com/pranavpandey/dynamic-utils'
    gitUrl = 'https://github.com/pranavpandey/dynamic-utils'
    issueUrl = 'https://github.com/pranavpandey/dynamic-utils/issues'
    githubUrl = 'pranavpandey/dynamic-utils'

    mavenRepo = 'android'
    mavenGroup = 'com.pranavpandey.android'
    mavenDir = 'com/pranavpandey/android'
    mavenArtifactId = 'dynamic-utils'
    mavenInceptionYear = '2017'
    mavenVersion = '4.7.0'
    mavenVersionCode = 48

    developerId = 'pranavpandey'
    developerName = 'Pranav Pandey'
    developerEmail = 'dynamic@pranavpandey.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    licenseDistribution = 'repo'
    allLicenses = ["Apache-2.0"]

    publication = 'local.properties'

    ext["signing.keyId"] = ''
    ext["signing.password"] = ''
    ext["signing.secretKeyRingFile"] = ''

    sonatypeUsername = ''
    sonatypePassword = ''
    sonatypeStagingProfileId = ''
}

apply plugin: 'io.github.gradle-nexus.publish-plugin'

File publish = project.rootProject.file("${publication}")
if (publish.exists()) {
    Properties properties = new Properties()
    new FileInputStream(publish).withCloseable { is -> properties.load(is) }
    properties.each { name, value -> ext[name] = value }
}

nexusPublishing {
    repositories {
        sonatype {
            username = sonatypeUsername
            password = sonatypePassword
            stagingProfileId = sonatypeStagingProfileId
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(
                    uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}
